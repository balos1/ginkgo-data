(
"Select the system name and executor to show the results for:";
$system := "isaitest";
$executor := "cuda";
$data := content[dataset.system=$system and dataset.executor=$executor];
$lib := $contains($executor, "cuda") ? "cusp" : "hipsp";
$turn_format := function ($str, $lib) {
    $replace($str, /sparselib|splib|cusp|hipsp/, $lib)
};
$formats := ["coo", "csr", "ell", "hybrid", "sellp",
  $turn_format("splib_coo", $lib),
  $turn_format("splib_csr", $lib),
  $turn_format("splib_ell", $lib),
  $turn_format("splib_hybrid", $lib)];

$solvers := ["idr-general-isai", "idr", "idr-parilu-isai", "idr-ilu-isai"];

$matrices := [
    "memplus",
    "poli_large",
    "cavity26",
    "FEM_3D_thermal2",
    "1138_bus",
    "crystm02",
    "cage11",
    "cage14",
    "stomach",
    "lung2",
    "torso2",
    "torso3",
    "trans4",
    "dc2",
    "chipcool1"
];

$getColor := function($num_colors, $id) {
  "hsl(" & $floor(360 * $id / $num_colors) & ",40%,55%)"
};

$plot_data := $solvers~>$map(function($v, $i) {{
  "label": $v,
  "data": $data.{
    "x": problem.name,
    "y": (solver~>$lookup($v)).apply.iterations
  },
  "backgroundColor": $solvers~>$count()~>$getColor($i)
}});

{
  "type": "scatter",
  "data": { "datasets": $plot_data },
  "options": {
    "title": {
      "display": true,
      "text": "Iteration Count"
    },
    "tooltips": {
      "mode": "index",
      "intersect": true
    },
    "scales": {
      "xAxes": [{
        "type": "category",
        "ticks": {
          "autoSkip": false
        },
        "labels": $matrices,
        "scaleLabel": {
          "display": true
        }
      }],
      "yAxes": [{ 
          "type": "logarithmic",
          "scaleLabel": {
          "display": true,
          "labelString": "iterations"
      }}]
    }
  }
}
)

