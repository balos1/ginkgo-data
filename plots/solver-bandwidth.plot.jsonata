(
"Select the system name and executor to show the results for:";
$data_hip := content[dataset.executor="hip"];
$data_cuda := content[dataset.executor="cuda"];

$solvers_hip := $data_hip.solver~>$keys();
$solvers_cuda := $data_cuda.solver~>$keys();

$matrices := [
"rajat31",
"atmosmodj",
"nlpkkt160",
"thermal2",
"CurlCurl_4",
"Bump_2911",
"Cube_Coup_dt0",
"StocF-1465",
"circuit5M",
"FullChip"];

$getColor := function($num_colors, $id) {
  "hsl(" & $floor(360 * $id / $num_colors) & ",40%,55%)"
};

$floor := $string ~> $substringBefore(?, '.') ~> $number;

$memops := function($name, $n, $nnz, $it, $k, $r) {
  ($name = "bicgstab" ? ((5*$n + 2*$nnz)*8 + 2*$nnz*4 + $it/2*((29*$n + 4*$nnz)*8 + 4*$nnz*4)+ ((10*$n + 6)*8 + $it*((8*$n + 5)*8))) :
   $name = "cg" ? ((4*$n + 2*$nnz)*8 + 2*$nnz*4 + $it*((15*$n + 2*$nnz)*8 + 2*$nnz*4)) + ((5*$n + 2)*8 + $it*((5 * $n + 2)*8)) :
   $name = "cgs" ? ((5*$n + 2*$nnz)*8 + 2*$nnz*4 + $it/2*((20*$n + 4*$nnz)*8 + 4*$nnz*4) + ((10*$n + 2)*8 + $it*((10*$n + 3)*8))) :
   $name = "fcg" ? ((4*$n + 2*$nnz)*8 + 2*$nnz*4 + $it*((17*$n + 2*$nnz)*8 + 2*$nnz*4) + ((6*$n + 3)*8 + $it*((6*$n + 3)*8))) :
   $name = "gmres"? (((($r*$r + 3*$r)/2 + 9*$n + 2*$nnz)*8 + 2*$nnz * 4 + $floor($it/$k) * (($k*$k/2 + 3*$k/2 + 2*$nnz + 9*$n)*8 + 2*$nnz*4) + $it*((3*$n + 8 + 2*$nnz)*8 + 2*$nnz*4) + ($floor($it/$k)*($k - 1)*$k/2 + ($r-1)*$r/2)*((2*$n + 5)*8)) + ((2 + 3*$k + 6*$n + $r)*8 + 8 + $floor($it/$k)*((2*$k + 6*$n + 1)*8 + 8) + $it*((4 * $n + 7)*8) + ($floor($it/$k)*($k-1)*$k/2 + ($r-1)*$r/2)*(($n+3)*8))) :
    -1000000000) / 1000000000
};


$plot_hip := $solvers_hip~>$map(function($v, $i) {{
  "label": $v,
  "data": $data_cuda.{
    "x": problem.name,
    "y": $memops($v, problem.rows, problem.nonzeros, (solver~>$lookup($v)).apply.iterations, 100, ((solver~>$lookup($v)).apply.iterations)%100) / ((solver~>$lookup($v)).apply.time/1000000000)
  },
  "backgroundColor": $solvers_hip~>$count()~>$getColor($i)
}});

{
  "type": "scatter",
  "data": { "datasets": $plot_hip },
  "options": {
    "title": {
      "display": true,
      "text": "Performance vs Nonzero Count"
    },
    "tooltips": {
      "mode": "index",
      "intersect": true
    },
    "scales": {
      "xAxes": [{
        "type": "category",
        "labels": $matrices,
        "ticks": {
          "autoSkip": false
        },
        "scaleLabel": {
          "display": true,
          "labelString": "Problem"
        }
      }],
      "yAxes": [{ "scaleLabel": {
          "display": true,
          "labelString": "Time (s)"
      }}]
    }
  }
}
)
