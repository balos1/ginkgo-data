(
"Select the system name and executor to show the results for:";
$system := "V100";
$executor := "cuda";
$data := content[dataset.system=$system and dataset.executor=$executor];
$lib := $contains($executor, "cuda") ? "cusp" : "hipsp";
$turn_format := function ($str, $lib) {
    $replace($str, /sparselib|splib|cusp|hipsp/, $lib)
};
$formats := ["coo", "csr", "ell", "hybrid", "sellp",
  $turn_format("splib_coo", $lib),
  $turn_format("splib_csr", $lib),
  $turn_format("splib_ell", $lib),
  $turn_format("splib_hybrid", $lib)];

$getColor := function($num_colors, $id) {
  "hsl(" & $floor(360 * $id / $num_colors) & ",40%,55%)"
};

$plot_data := $formats~>$map(function($v, $i) {{
  "label": $v,
  "data": $data.{
    "x": problem.nonzeros,
    "y": 2 * problem.nonzeros / (spmv~>$lookup($v)).time
  },
  "backgroundColor": $formats~>$count()~>$getColor($i)
}});

{
  "type": "scatter",
  "data": { "datasets": $plot_data },
  "options": {
    "title": {
      "display": true,
      "text": "Performance vs Nonzero Count"
    },
    "tooltips": {
      "mode": "index",
      "intersect": true
    },
    "scales": {
      "xAxes": [{
        "type": "logarithmic",
        "scaleLabel": {
          "display": true,
          "labelString": "#nonzeros"
        }
      }],
      "yAxes": [{ "scaleLabel": {
          "display": true,
          "labelString": "GFLOP/s"
      }}]
    }
  }
}
)
