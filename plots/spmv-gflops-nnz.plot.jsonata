(
"Select the system name and executor to show the results for:";
$data := content[dataset.system="K20Xm" and dataset.executor="cuda"];
$getColor := function($num_colors, $id) {
  "hsl(" & $floor(360 * $id / $num_colors) & ",40%,55%)"
};

$formats := $data.spmv~>$keys();
$plot_data := $formats~>$map(function($v, $i) {{
  "label": $v,
  "data": $data.{
    "x": problem.nonzeros,
    "y": 2 * problem.nonzeros / (spmv~>$lookup($v)).time
  },
  "backgroundColor": $formats~>$count()~>$getColor($i)
}});

{
  "type": "scatter",
  "data": { "datasets": $plot_data },
  "options": {
    "title": {
      "display": true,
      "text": "Performance vs Nonzero Count"
    },
    "tooltips": {
      "mode": "index",
      "intersect": true
    },
    "scales": {
      "xAxes": [{
        "type": "logarithmic",
        "scaleLabel": {
          "display": true,
          "labelString": "#nonzeros"
        }
      }],
      "yAxes": [{ "scaleLabel": {
          "display": true,
          "labelString": "GFLOP/s"
      }}]
    }
  }
}
)
