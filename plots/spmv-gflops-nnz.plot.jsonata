(
$comment := "Select the system name and executor to show the results for below:";
$data := content[dataset.system="K20Xm" and dataset.executor="cuda"];
$formats := content.spmv~>$keys()~>$filter(function ($v) { $v != "optimal_format" });
{
  "type": "scatter",
  "data": {
    "labels": $data.problem.nonzeros,
    "datasets": $formats~>$map(function ($v, $i, $a) {(
      $hue := $floor(360 * $i / $count($a));
      $color := "hsl(" & $hue & ",50%,60%)";
      {
        "label": $v,
        "data": $data.{
          "x": problem.nonzeros,
          "y": 2 * problem.nonzeros / (spmv~>$lookup($v)).time * 1e-9
        },
        "backgroundColor": $color,
        "fill": false,
        "showLine": false
      })})
  },
  "options": {
    "title": {
      "display": true,
      "text": "GFLOPs comparison plot"
    },
    "tooltips": {
      "mode": "index",
      "intersect": true
    },
    "scales": {
      "xAxes": [
        {
          "type": "logarithmic",
          "scaleLabel": {
            "display": true,
            "labelString": "Number of nonzeros"
          }
        }
      ],
      "yAxes": [
        {
          "scaleLabel": {
            "display": true,
            "labelString": "GFLOPs"
          },
          "ticks": {
            "min": 0
          }
        }
      ]
    }
  }
}
)
